# -*- Mode: HCL; -*-

resource "aws_instance" "service-ami" {
  depends_on = [
    aws_route.main,
    aws_main_route_table_association.main,
    aws_network_acl_rule.private-i_x_all,
    aws_network_acl_rule.private-e_x_all,
    aws_network_acl_rule.private-i_g_tcpephemeral,
    aws_network_acl_rule.private-i_g_udpephemeral,
    aws_network_acl_rule.private-e_g_all,
    aws_efs_mount_target.main,
  ]

  instance_type        = local.instances_service.instance_type
  iam_instance_profile = aws_iam_instance_profile.ec2.name
  user_data            = file(local.instances_service.user_data)

  vpc_security_group_ids = aws_security_group.private.*.id
  subnet_id              = aws_subnet.private[0].id

  associate_public_ip_address = true

  ami      = data.aws_ami_ids.ubuntu.ids[0]
  key_name = local.instances_service.key_name

  root_block_device {
    volume_size = local.instances_service.root_volume_size
  }

  tags = merge(local.common_tags, {
    "Name" = "${local.default_name}-service-ami"
  })
}

resource "aws_ami_from_instance" "service-ami" {
  name = "${local.default_name}.service.${formatdate("YYYY-MM-DD'T'hhmmssZZZ", timestamp())}"
  source_instance_id = aws_instance.service-ami.id
  tags = merge(local.common_tags, {
    "Name" = "${local.default_name}.service.${formatdate("YYYY-MM-DD'T'hhmmssZZZ", timestamp())}"
  })
}
